//event which controll all effects(smoke/particle/etc)
class pb_effect_event : eventhandler
{
enum effect_type
{
	sprite,
	particle
};

//arrays for storing pointers to sprite a particle actors
protected array<sprite_effect> sprite_actors;
protected array<particle_effect> particle_actors;

//special array type which contains thinkers which handle case of contaniou rays
//like beam bfg
protected array<thinker> continious_ray_handlers;

//give pointer to this event to pb actors
//so they can use functions of that event whenever they want
override void worldthingspawned(worldevent e)
{	
	if(e.thing is "pb_weapon_base")
	{pb_weapon_base(e.thing).effects = self;}

	if(e.thing is "pb_monster_base")
	{pb_monster_base(e.thing).effects = self;}
/*/
	if(e.thing is "pb_projectile_base")
	{ canonballs.push((pb_projectile_base)(e.thing) );}
	/*/
}


//find bored thinker to say it "draw some line, cmon"
thinker find_free_line_handler()
{
	int dd = self.continious_ray_handlers.size();

	for(int i = 0; i <= dd; i++)
	{
		if(i == dd)
		{
			thinker ss = new("constant_line_handler");
			self.continious_ray_handlers.push(ss);
			constant_line_handler(ss).effect = self;
			return ss; break;
		}
		if(self.continious_ray_handlers[i])
		{
			if(constant_line_handler(self.continious_ray_handlers[i]).busy == false)
			{
				return self.continious_ray_handlers[i]; break; 
			}
		}
	}
	return null;
}

//find free sprite or particle effect actor
actor find_free_effect_actor(effect_type what_effect = sprite)
{
	if(what_effect == sprite)
	{
		int dd = self.sprite_actors.size();

		for(int i = 0; i <= dd; i++)
		{
			if(i == dd)
			{
				actor ss = actor.spawn("sprite_effect", pos:(0, 0, 0) );
				sprite_actors.push((sprite_effect)(ss) );
				//since zscript compiler is stupid as fuck, it just bytecod interpreter
				//all previous checks and actor type just too complicated for it
				//so we need to ensure it that pushed actor match with array type
				//to do so use(actor type)(pointer to actor)
				//CAUTION space between )( break it
				return ss; break;
			}
			if(self.sprite_actors[i])
			{
				if(sprite_effect(self.sprite_actors[i]).check_busyness() == false)
				{
					return self.sprite_actors[i]; break; 
				}
			}
		}
	}

	if(what_effect == particle)
	{
		int dd = self.particle_actors.size();

		for(int i = 0; i <= dd; i++)
		{
			if(i == dd)
			{
				actor ss = actor.spawn("particle_effect", pos:(0, 0, 0) );
				particle_actors.push((particle_effect)(ss) );
				return ss; break;
			}
			if(self.particle_actors[i])
			{
				if(particle_effect(self.particle_actors[i]).check_busyness() == false)
				{
					return self.particle_actors[i]; break; 
				}
			}
		}
	}
	return null;
}

//simple function all sense of it check state and say into console if there are no such state
void approve_state(actor subject = null, statelabel state_name = null)
{
	if(subject == null) {console.printf("there are no actor to change state"); return;}
	if(state_name == null) {console.printf("there are no state to search"); return;}

	let stt_lnk = subject.findstate(state_name);

	if(stt_lnk == null) {console.printf("actor at %d %d %d dont have state", subject.pos.x, subject.pos.y, subject.pos.z ); return;}

	subject.setstate(stt_lnk);
}

//here actually shows some mikle bay explosions
actor show_some_effect(actor caller = null, statelabel effect_state = null, vector3 eff_vel =(0,0,0) , effect_type effect_type = sprite)
{
	//effect must be "attached" to some actor
	if(caller == null) {return null;}
	
	actor lin = find_free_effect_actor(effect_type);
	approve_state(lin, effect_state);
	lin.setorigin(caller.pos, false);
	lin.vel = eff_vel;
	return lin;
}

}

//dummy class which serve only to combine all effects under one base
class actual_effect_base : pb_special_effect_base {}

class sprite_effect : actual_effect_base {}
class particle_effect : actual_effect_base {}





















// Optional Motion Blur by Pixel Eater
class MBlurHandler : StaticEventHandler
{
	int			pitch, yaw ;
	double		xtravel, ytravel ;
	
	transient cvar m_blur;
	
	override void PlayerEntered( PlayerEvent e )
	{
		PlayerInfo plr = players[ consoleplayer ];
		m_blur = Cvar.GetCVar( "mblur", plr );
		if( plr )
		{	
			xtravel = 0 ;
			ytravel = 0 ;
		}
	}
	
	override void WorldTick()
	{
		PlayerInfo	plr = players[ consoleplayer ];
		if( plr && plr.health > 0 && m_blur.GetBool() )
		{
			yaw		= plr.mo.GetPlayerInput( ModInput_Yaw );
			pitch	= -plr.mo.GetPlayerInput( ModInput_Pitch );
		}
	}
	
	override void NetworkProcess( ConsoleEvent e )
	{
		PlayerInfo	plr = players[ consoleplayer ];
		if( plr && e.Name == "liveupdate" )
		{
			double pitchdimin	= 1. - abs( plr.mo.pitch * 1. / 90 );
			double decay		= 1. - Cvar.GetCVar( "mblur_recovery", plr ).GetFloat() * .01 ;
			double amount		= Cvar.GetCVar( "mblur_strength", plr ).GetFloat() * 10. / 32767 * sqrt( pitchdimin );
			xtravel				= xtravel * decay + yaw * amount * .625 ;
			ytravel				= ytravel * decay + pitch * amount ;
			
			if( Cvar.GetCVar( "mblur_autostop", plr ).GetBool() )
			{
				double threshold = Cvar.GetCVar( "mblur_threshold", plr ).GetFloat() * 30 ;
				double recovery2 = 1 - Cvar.GetCVar( "mblur_recovery2", plr ).GetFloat() * .01 ;
				if( abs( yaw )		<= threshold ) xtravel *= recovery2 ;
				if( abs( pitch )	<= threshold ) ytravel *= recovery2 ;
			}
		}
	}
	
	override void UiTick()
	{
		PlayerInfo	plr = players[ consoleplayer ];
		if( plr )
		{
			if( plr.health > 0 && Cvar.GetCVar( "mblur", plr ).GetBool() )//&& yaw && pitch )
			{
				EventHandler.SendNetworkEvent( "liveupdate" );
				
				int copies			= 1 + Cvar.GetCVar( "mblur_samples", plr ).GetInt() ;
				double increment	= 1. / copies ;
				vector2 travel		=( xtravel, ytravel ) / screen.getheight() ;
				
				Shader.SetUniform2f( plr, "MBlur", "steps", travel * increment );
				Shader.SetUniform1f( plr, "MBlur", "samples", copies );
				Shader.SetUniform1f( plr, "MBlur", "increment", increment );
				Shader.SetUniform1f( plr, "MBlur", "blendmode", Cvar.GetCVar( "mblur_blendmode", plr ).GetInt() );
					
				Shader.SetEnabled( plr, "MBlur", true );
			}
			else
			{
				Shader.SetEnabled( plr, "MBlur", false );
			}
		}
	}
}