Actor SilencerEquipped: Inventory
{
	Inventory.maxamount 1
}

Actor PBPistolWasEmpty: Inventory
{
	Inventory.maxamount 1
}

Actor PBPistolHasUnloaded: Inventory
{
	Inventory.maxamount 1
}

ACTOR SwitchingFromDualWieldPistols : Inventory
{
Inventory.MaxAmount 1
}

ACTOR SwitchingToDualWieldPistols: Inventory
{
Inventory.MaxAmount 1
}

ACTOR DualWieldingPistols: Inventory
{
Inventory.MaxAmount 1
}

ACTOR BDPistol : PB_Weapon //Replaces Pistol
{
	SpawnID 9220
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.4
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 5
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "PistolBullets"
	Weapon.AmmoType2 "PrimaryPistolAmmo"
	+FLOORCLIP
	+DONTGIB
	Obituary "%o was shot down by %k's Pistol"
    AttackSound "None"
	Tag "UAC .45 Pistol"
    Inventory.PickupSound "weapons/pistolup"
	Inventory.Pickupmessage "You got the UAC .45 Pistol! (Slot 2)"
	+WEAPON.WIMPY_WEAPON
    +WEAPON.NOAUTOAIM
    +WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	+WEAPON.NO_AUTO_SWITCH
    Scale 0.80

	States
	{
	Spawn:
		TNT1 A 0 A_SPawnItem("Handgun")
		TNT1 A 0 A_SpawnItem("HasTwoPistols")
		DEGT A -1
		Stop 
			
	Steady:
		TNT1 A 1 A_JumpIfInventory("GoFatality", 1, "Steady")
		Goto Ready
		
	FastSwitchFromDualWield2:
		TNT1 A 0 A_TakeInventory("SwitchingFromDualWieldPistols", 1)
		TNT1 A 0 A_PlaySoundEx("IronSights", "Auto")
		D2GS JK 1 A_SetRoll(roll-0.2)
		D2GS LM 1 A_SetRoll(roll+0.2)
		Goto Ready3
	Ready:
		TNT1 A 0 A_JumpIfInventory("DualWieldingPistols", 1, "WeaponSpecial")
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"ReadySilencer")
		TNT1 A 0 A_JumpIfInventory("SwitchingFromDualWieldPistols", 1, "FastSwitchFromDualWield2")
		DEGG E 1 A_PlaySound("weapons/pistolup")
        DEGG DCB 1 A_JumpIfInventory("GoFatality", 1, "Steady")
	Ready3:
		TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 A_TakeInventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"ReadyToFireSilencer")
		TNT1 A 0 A_JumpIfInventory("PBPistolWasEmpty", 1, "LoadChamber")
	ReadyToFire:
		DEGG A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		Goto ReadyToFire
		
	ReadySilencer:
		D3GG E 1 A_PlaySound("weapons/pistolup")
        D3GG DCB 1 A_JumpIfInventory("GoFatality", 1, "Steady")
	ReadyToFireSilencer:
		TNT1 A 0 A_JumpIfInventory("PBPistolWasEmpty", 1, "LoadChamberSilencer")
		D3GG A 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD,PBWEAP_ISPISTOLSILENCERSTATE)
		Goto ReadyToFireSilencer
		
	Deselect:	    
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,6)
		TNT1 A 0 A_JumpIfInventory("GotMeatShield", 1, "GrabEnemy")
		TNT1 A 0 A_JumpIfInventory("SwitchingToDualWieldPistols", 1, 11)
        DEGG BCDE 1		
		TNT1 A 0 A_Jump(256, 5)
        D3GG BCDE 1		
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		Wait
		
	WeaponSpecial:
		TNT1 A 0 A_JumpIfInventory("DualPistolChecker", 1,"SwitchToDualWield")
		TNT1 A 0 { 
			A_Print("You need two pistols to dual wield!");
			A_Takeinventory("GoWeaponSpecialAbility",1);
			}
		Goto ReadyToFire
	SwitchToDualWield:
		TNT1 A 0 A_Giveinventory("SwitchingToDualWieldPistols",1)
		TNT1 A 0 A_SelectWeapon("Handgun")
		Goto Select
		
	Select:
		TNT1 A 0
		TNT1 A 0 A_Overlay(-10, "FirstPersonLegsStand")
		TNT1 A 0 ACS_NamedExecuteAlways("ToggleRevolverUpgraded",0)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_TakeInventory("RifleSelected",1)
		TNT1 A 0 A_TakeInventory("CarbineSelected",1)
		TNT1 A 0 A_TakeInventory("ADSMode",1)
		TNT1 A 0 A_TakeInventory("FistsSelected",1)
		TNT1 A 0 A_TakeInventory("SawSelected",1)
		TNT1 A 0 A_TakeInventory("SledgeSelected",1)
		TNT1 A 0 A_TakeInventory("DualHandgunSelected",1)
		TNT1 A 0 A_TakeInventory("RevolverSelected",1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected",1)
		TNT1 A 0 A_TakeInventory("SSGSelected",1)
		TNT1 A 0 A_TakeInventory("ASGSelected",1)
		TNT1 A 0 A_TakeInventory("QSGSelected",1)
		TNT1 A 0 A_TakeInventory("MinigunSelected",1)
		TNT1 A 0 A_TakeInventory("NewChaingunSelected",1)	
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("SGLSelected",1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected",1)
		TNT1 A 0 A_TakeInventory("M2Selected",1)	
		TNT1 A 0 A_TakeInventory("FreezerSelected",1)
		TNT1 A 0 A_TakeInventory("RailGunSelected",1)
		TNT1 A 0 A_TakeInventory("BFGSelected",1)
		TNT1 A 0 A_TakeInventory("BFGBeamSelected",1)
		TNT1 A 0 A_TakeInventory("BHCSelected",1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected",1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("HellRifleSelected",1)
		TNT1 A 0 A_TakeInventory("LandMineSelected",1)
		TNT1 A 0 A_TakeInventory("UACSMGSelected",1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected",1)
		TNT1 A 0 A_TakeInventory("LostSoulSelected",1)
		TNT1 A 0 A_TakeInventory("HasBarrel",1)
		TNT1 A 0 A_TakeInventory("HasBurningBarrel",1)
		TNT1 A 0 A_Takeinventory("HasPlasmaWeapon",1)
		TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
		TNT1 A 0 A_GiveInventory("HandgunSelected",1)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise
		TNT1 AAAAAAAA 1 A_Raise
		Wait
		
	Fire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"FireSilencer")
        TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,1)
        Goto Reload
		DEGF A 1 BRIGHT { 
			A_FireBullets (0.1, 0.1, -1, 12, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_ZoomFactor(0.985);
			A_AlertMonsters;
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("ShakeYourAssMinor", 0, 0, 0, 0);
			A_Takeinventory("PrimaryPistolAmmo",1);
			A_FireCustomMissile("Mp40CaseSpawn", 0,0,-1,-2);
			A_PlaySoundEx("weapons/pistol", "Weapon");
			A_FireCustomMissile("YellowFlareSpawn",0,0,0,0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.09);
				A_SetAngle(angle-0.04);
				}
			else {
				A_SetPitch(pitch-0.18);
				A_SetAngle(angle-0.08);
				}
		}
        DEGF B 1 {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.09);
				A_SetAngle(angle-0.04);
				}
			else {
				A_SetPitch(pitch-0.18);
				A_SetAngle(angle-0.08);
				}
		}
        DEGF B 1 {
			A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.09);
				A_SetAngle(angle-0.04);
				}
			else {
				A_SetPitch(pitch-0.18);
				A_SetAngle(angle-0.08);
				}
		}
		DEGF C 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		DEGG AAAA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		TNT1 A 0 A_Refire
		Goto ReadyToFire
		
	FireSilencer:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,1)
        Goto ReloadSilencer
		D3GF A 1 BRIGHT {
			A_FireBullets (0.1, 0.1, -1, 10, "HitPuff", FBF_NORANDOM,8192,"Tracer", -2,0);
			A_ZoomFactor(0.985);
			A_FireCustomMissile("GunFireSmoke", 0, 0, 0, 0, 0, 0);
			A_FireCustomMissile("ShakeYourAssMinor", 270, 0, 0, 0);
			A_Takeinventory("PrimaryPistolAmmo",1);
			A_FireCustomMissile("Mp40CaseSpawn", 0,0,-1,-2);
			A_PlaySoundEx("weapons/suppressedpistol", "Weapon");
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.09);
				A_SetAngle(angle-0.04);
				}
			else {
				A_SetPitch(pitch-0.18);
				A_SetAngle(angle-0.08);
				}
		}	
        D3GF B 1 {
			A_ZoomFactor(0.99);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.09);
				A_SetAngle(angle-0.04);
				}
			else {
				A_SetPitch(pitch-0.18);
				A_SetAngle(angle-0.08);
				}
		}
        D3GF B 1 {
			A_ZoomFactor(1.0);
			if (CountInv("PowerStrength") >= 1 ) {
				A_SetPitch(pitch-0.09);
				A_SetAngle(angle-0.04);
				}
			else {
				A_SetPitch(pitch-0.18);
				A_SetAngle(angle-0.08);
				}
		}
		D3GF C 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		D3GG AAAA 1 A_DoPBWeaponAction(WRF_ALLOWRELOAD)
		D3GG A 0 A_Refire
        Goto ReadyToFireSilencer
		
	AltFire:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"DetachSilencer")
		TNT1 A 0 A_GiveInventory ("SilencerEquipped",1)
		D5GA ABCDEFFFGHIJKLM 1 A_SetRoll(roll+.2)
		TNT1 A 0 A_PlaySoundEx("weapons/pistolsuppressor_on", "Auto")
		D5GA NNNOPOPOQRRRSSS 1 A_SetRoll(roll-.2)
		TNT1 A 0 A_PlaySoundEx("weapons/pistolup", "Auto")
		D5GA TUVWXY 1
		TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 A_TakeInventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_Print("Silencer Attached")
		Goto ReadyToFireSilencer
		
	DetachSilencer:
		TNT1 A 0 A_TakeInventory ("SilencerEquipped",1)
		D5GF ABCDEFG 1 A_SetRoll(roll+.2)
		TNT1 A 0 A_PlaySoundEx("weapons/pistolsuppressor_off", "Auto")
		D5GF FHIJJKKLLMMNOP 1 A_SetRoll(roll-.1)
		D5GF QRSTUUUVWXYZ 1
		D5GG AB 1
		TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 A_TakeInventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_Print("Silencer Detached")
		Goto ReadyToFire
		
		
	NoAmmoSilencer:
		D3GG A 1 A_PlaySoundEx("weapons/empty", "Auto")
		Goto ReadyToFireSilencer
	NoAmmo:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"NoAmmoSilencer")
		DEGG A 1 A_PlaySoundEx("weapons/empty", "Auto")
		Goto ReadyToFire
	Reload:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"ReloadSilencer")
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PistolBullets",1,1)
        Goto NoAmmo
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto")
        D5GB A 1 A_SetRoll(roll+.5) 
		TNT1 A 0 A_JumpIfInventory("PBPistolHasUnloaded", 1, 11)
		TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",5,0,8,-4)
		D5GB BCD 1 A_SetRoll(roll+.5)
		D5GB EFGHI 1 A_SetRoll(roll-.5)
		TNT1 A 0 A_PlaySoundEx("PSRLIN", "Auto")
		D5GB KKLMNOPQRS 1 A_SetRoll(roll-.1)
		D5GB TUVWX 1 A_SetRoll(roll+0.2)
		D5GB Y 1 A_SetRoll(0)
		TNT1 A 0 A_Takeinventory("PBPistolHasUnloaded",1)
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,"InsertBulletsNoChamber")//16+1 effect
		TNT1 A 0 A_GiveInventory("PBPistolWasEmpty", 1)
	InsertBulletsChamber:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16, "Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets", 1, 1)
		Goto Ready3
		TNT1 A 0 {	
			A_Giveinventory("PrimaryPistolAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
	InsertBulletsNoChamber:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16, "Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets", 1, 1)
		Goto Ready3
		TNT1 A 0 {	
			A_Giveinventory("PrimaryPistolAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
		
	LoadChamber:
		TNT1 A 0 A_PlaySoundEx("PSRLFIN", "Auto")
		D5GC CDEF 1 A_SetRoll(roll+.3)
		D5GC GGHH 1 A_SetRoll(roll+.3)
		D5GC I 1
		D5GC JKLMNOPQ 1 A_SetRoll(roll-.3)
		TNT1 A 0 A_Takeinventory("PBPistolWasEmpty",1)
		Goto Ready3
		
	ReloadSilencer:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16,"Ready3")
        TNT1 A 0 A_JumpIfInventory("PistolBullets",1,1)
        Goto NoAmmoSilencer
		TNT1 A 0 A_Giveinventory("PB_LockScreenTilt",1)
		TNT1 A 0 A_PlaySoundEx("PSRLOUT", "Auto")
        D5GD A 1 A_SetRoll(roll+.5) 
		TNT1 A 0 A_JumpIfInventory("PBPistolHasUnloaded", 1, 11)
		TNT1 A 0 A_FireCustomMissile("EmptyClipMP40",5,0,8,-4)
		D5GD BCD 1 A_SetRoll(roll+.5)
		D5GD EFGHI 1 A_SetRoll(roll-.5)
		TNT1 A 0 A_PlaySoundEx("PSRLIN", "Auto")
		D5GD KKLMNOPQRS 1 A_SetRoll(roll-.1)
		D5GD TUVWX 1 A_SetRoll(roll+0.2)
		D5GD Y 1 A_SetRoll(0)
		TNT1 A 0 A_Takeinventory("PBPistolHasUnloaded",1)
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,"InsertBulletsNoChamber")//16+1 effect
		TNT1 A 0 A_GiveInventory("PBPistolWasEmpty", 1)
	InsertBulletsSilencerChamber:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16, "Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets", 1, 1)
		Goto Ready3
		TNT1 A 0 {	
			A_Giveinventory("PrimaryPistolAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
		
	InsertBulletsSilencerNoChamber:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",16, "Ready3")
		TNT1 A 0 A_JumpIfInventory("PistolBullets", 1, 1)
		Goto Ready3
		TNT1 A 0 {	
			A_Giveinventory("PrimaryPistolAmmo",1);
			A_Takeinventory("PistolBullets",1);
			}
		Loop
		
	LoadChamberSilencer:
		TNT1 A 0 A_PlaySoundEx("PSRLFIN", "Auto")
		D5GE CDEF 1 A_SetRoll(roll+.3)
		D5GE GGHH 1 A_SetRoll(roll+.3)
		D5GE I 1
		D5GE JKLMNOPQ 1 A_SetRoll(roll-.3)
		TNT1 A 0 A_Takeinventory("PBPistolWasEmpty",1)
		Goto Ready3
		
	// Unloading States
		
	AlreadyUnloaded:
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto ReadyToFire
		
	Unload:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"UnloadSilencer")
		TNT1 A 0 A_JumpIfInventory("PBPistolHasUnloaded", 1, "AlreadyUnloaded")
        TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,1)
        Goto NoAmmo
		D5GB A 1 
		TNT1 A 0 A_PlaySound("PSRLOUT", 1)
        D5GB BCDEFGHIJKDCBA 1
		TNT1 A 0 A_Takeinventory("Unloading",1)
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,1)
		Goto FinishUnload
		TNT1 A 0 {
			A_Takeinventory("PrimaryPistolAmmo",1);
			A_Giveinventory("PistolBullets",1);
			}
		Loop
	FinishUnload:
		TNT1 A 0 A_PlaySound("DryFire")
		TNT1 A 0 A_GiveInventory("PBPistolHasUnloaded", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto ReadyToFire
		
	UnloadSilencer:
		TNT1 A 0 A_JumpIfInventory("PBPistolHasUnloaded", 1, "AlreadyUnloaded")
        TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,1)
        Goto NoAmmo
		D5GD A 1 
		TNT1 A 0 A_PlaySound("PSRLOUT", 1)
        D5GD BCDEFGHIJKDCBA 1
		Goto RemoveBulletsSilencer
		
	RemoveBulletsSilencer:
		TNT1 A 0 A_JumpIfInventory("PrimaryPistolAmmo",1,1)
		Goto FinishUnloadSilencer
		TNT1 A 0 {
			A_Takeinventory("PrimaryPistolAmmo",1);
			A_Giveinventory("PistolBullets",1);
			}
		Goto RemoveBulletsSilencer
		
	FinishUnloadSilencer:
		TNT1 A 0 A_PlaySound("DryFire")
		TNT1 A 0 A_GiveInventory("PBPistolHasUnloaded", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto ReadyToFireSilencer
		
	Spawn:
		DEGT A -1
		Stop
		
	// Flash States
		
	FlashKicking:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"FlashKickingSilencer")
		D5GU ABCDEFGHIJKLMN 1
		DEGG AAAA 1 // Pause?
		Stop
		
	FlashKickingSilencer:
		D5GV ABCDEFGHIJKLMN 1
		D3GG AAAA 1 // Pause?
		Stop
		
	FlashAirKicking:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"FlashAirKickingSilencer")
		D5GU ABCDEFGHIJKLMN 1
		DEGG AAAA 1 // Pause?
		Stop
		
	FlashAirKickingSilencer:
		D5GV ABCDEFGHIJKLMN 1
		D3GG AAAA 1 // Pause?
		Stop
		
	FlashSlideKicking:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"FlashSlideKickingSilencer")
		D5GY ABCDEFGHIJKLMNOPQRSTUV 1
		Stop
		
	FlashSlideKickingSilencer:
		D5GZ ABCDEFGHIJKLMNOPQRSTUV 1
		Stop
		
	FlashSlideKickingStop:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"FlashSlideKickingStopSilencer")
		D5GY WXYZ 1 
		DEGS A 1 
		Stop
		
	FlashSlideKickingStopSilencer:
		D5GZ WXYZ 1 
		D3GG A 1 
		Stop
		
	FlashPunching:
		TNT1 A 0 A_JumpIfInventory("SilencerEquipped",1,"FlashPunchingSilencer")
		D5GS ABCDEFGHIJKLMN 1
		DEGG AAAA 1 // Pause?
		Stop
		
	FlashPunchingSilencer:
		D5GT ABCDEFGHIJKLMN 1
		D3GG AAAA 1 // Pause?
		Stop
	}
}

ACTOR PrimaryPistolAmmo: Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 16
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 16
   Inventory.Icon "DEGTA0"
}
